buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'edu.sc.seis.gradle:launch4j:2.4.4'
    }
}

apply plugin:'groovy'
apply plugin:'maven'
apply plugin:'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'edu.sc.seis.launch4j'

group 'com.exictos.devops'
version '1.5.1-SNAPSHOT'

repositories {
    maven{
        url "${nexusUrl}/repository/dcs-maven/"
        credentials{
            username = nexusUsername
            password = nexusPassword
        }
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    // https://mvnrepository.com/artifact/org.codehaus.janino/janino
    compile group: 'org.codehaus.janino', name: 'janino', version: '3.0.8'
    // https://mvnrepository.com/artifact/org.wildfly.core/wildfly-cli
    compile group: 'org.wildfly.core', name: 'wildfly-cli', version: '4.0.0.Final'
    compile group: 'com.bloidonia', name: 'groovy-common-extensions', version:'0.7.0'
    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
}

uploadArchives {
    repositories {
        mavenDeployer {
            def url = "${nexusUrl}/repository/dcs-maven-"
            if(project.version.toString().contains("-SNAPSHOT"))
                url += "snapshots"
            else
                url += "releases"

            repository(url: url) {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
            pom.version = project.version
            pom.artifactId = project.name
            pom.groupId = project.group
        }
    }
}

groovydoc{
    docTitle = "Libero API"
    includePrivate = true
}

jar {
    manifest{
        attributes 'Main-Class': 'com.exictos.devops.Main'
    }
}

launch4j {
    mainClassName = 'com.exictos.devops.Main'
    headerType = "console"
    jar = "${rootDir}/build/libs/${project.tasks.shadowJar.archiveName}"
}